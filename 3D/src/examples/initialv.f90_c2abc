! Initial condition for the velocity.
!
! This file contains the expression used for the external
! mechanical forcing. You can use temporary real arrays
! R1-R3 of size (1:nx,1:ny,ksta:kend) and temporary complex
! arrays C1-C8 of size (nz,ny,ista:iend) to do intermediate
! computations. The variable u0 should control the global
! amplitude of the forcing, and variables fparam0-9 can be
! used to control the amplitudes of individual terms. At the
! end, the three components of the forcing in spectral
! space should be stored in the arrays fx, fy, and fz.

! Superposition of 2-component AB(C) vortices (with no 
! z-velocity component). The flow is perturbed by 2D 
! white-distributed random noise, with a specified level 
! relative to energy.
!     kdn : minimum AB(C) superposition wave number
!     kup : maximum AB(C) superposition wave number
!     u0  : velocity magnitude
!     vparam0: level of noise
!     vparam2: reference wave number, k0
!     vparam3: A amplitude
!     vparam4: B amplitude
!     vparam5: C amplitude

      IF ( (abs(Lx-Ly).gt.tiny).or.(abs(Lx-Lz).gt.tiny) ) THEN
        IF (myrank.eq.0) &
           PRINT *,'ABC initial conditions require Lx=Ly=Lz'
        STOP
      ENDIF

      R1 = 0.0_GP
      R2 = 0.0_GP
      R3 = 0.0_GP
!$omp parallel do if (kend-ksta.ge.nth) private (j,i)
      DO k = ksta,kend
!$omp parallel do if (kend-ksta.lt.nth) private (i)
         DO j = 1,ny
            DO i = 1,nx

               DO ki = INT(kdn),INT(kup)

               R1(i,j,k) = R1(i,j,k)+(vparam4*COS(2*pi*ki*(real(j,kind=GP)-1)/ &
                          real(ny,kind=GP))+vparam5*SIN(2*pi*ki*(real(k,kind=GP)-1)/   &
                          real(nz,kind=GP)))/ki**2
               R2(i,j,k) = R2(i,j,k)+(vparam3*SIN(2*pi*ki*(real(i,kind=GP)-1)/ &
                          real(nx,kind=GP))+vparam5*COS(2*pi*ki*(real(k,kind=GP)-1)/   &
                          real(nz,kind=GP)))/ki**2

               END DO
            END DO
         END DO
      END DO
      vz = 0.0_GP

      CALL fftp3d_real_to_complex(planrc,R1,vx,MPI_COMM_WORLD)
      CALL fftp3d_real_to_complex(planrc,R2,vy,MPI_COMM_WORLD)
      CALL normalize(vx,vy,vz,u0,1,MPI_COMM_WORLD)

      C1 = 0.0_GP
      C2 = 0.0_GP
      C3 = 0.0_GP

! Generate divergence-free 2C noise:
      tmp = 1.0_GP/(real(nx,kind=GP)*real(ny,kind=GP)*real(nz,kind=GP))

! To ensure Div v = 0: Set noise, Ny, s.t. kx Nx + ky Ny=0:
      IF (ista.eq.1) THEN
!$omp parallel do
        DO j = 2,ny/2+1
          dump = 0.0_GP
          IF ( (kk2(1,j,1).ge.vparam2**2) ) THEN
            rmq  = sqrt(kk2(1,j,1))
!           dump = exp(-log10((rmq/vparam2)**2)/(2.0*vparam3**2))/rmq**2
            dump = 1.0_GP/rmq**3
          ENDIF
          phase = 2.0*pi*randu(seed); 
          cdump = (cos(phase)+im*sin(phase))*dump
          IF ( ky(j).GT.tiny ) THEN
          C1     (1,j,1) = cdump
          C1(1,ny-j+2,1) = conjg(C1(1,j,1))
          C2     (1,j,1) = -cdump*kx(1)/ky(j)
          C2(1,ny-j+2,1) = conjg(C2(1,j,1))
          ENDIF
        END DO
!$omp parallel do
        DO k = 2,nz/2+1
          dump = 0.0_GP
          IF ( (kk2(k,1,1).ge.vparam2**2) ) THEN
             rmq  = sqrt(kk2(k,1,1))
!            dump = exp(-log10((rmq/vparam2)**2)/(2.0*vparam3**2))/rmq**2
             dump = 1.0_GP/rmq**3
          ENDIF
          phase = 2.0*pi*randu(seed); 
          cdump = (cos(phase)+im*sin(phase))*dump
          IF ( ky(1).GT.tiny ) THEN
          C1     (k,1,1) = cdump
          C1(nz-k+2,1,1) = conjg(C1(k,1,1))
          C2     (k,1,1) = -cdump*kx(1)/ky(1)
          C2(nz-k+2,1,1) = conjg(C2(k,1,1))
          ENDIF
        END DO
!$omp parallel do private (k)
        DO j = 2,ny
          DO k = 2,nz/2+1
            dump = 0.0_GP
            IF ( (kk2(k,j,1).ge.vparam2**2) ) THEN
              rmq  = sqrt(kk2(k,j,1))
!             dump = exp(-log10((rmq/vparam2)**2)/(2.0*vparam3**2))/rmq**2
              dump = 1.0_GP/rmq**3
            ENDIF
            phase = 2.0*pi*randu(seed); 
            cdump = (cos(phase)+im*sin(phase))*dump
            IF ( ky(j).GT.tiny ) THEN
            C1          (k,j,1) = cdump
            C1(nz-k+2,ny-j+2,1) = conjg(C1(k,j,1))
            C2          (k,j,1) = -cdump*kx(1)/ky(j)
            C2(nz-k+2,ny-j+2,1) = conjg(C2(k,j,1))
            ENDIF
          END DO
        END DO
!$omp parallel do if (iend-2.ge.nth) private (j,k)
        DO i = 2,iend
!$omp parallel do if (iend-2.lt.nth) private (k)
          DO j = 1,ny
            DO k = 1,nz
              dump = 0.0_GP
              IF ( (kk2(k,j,i).ge.vparam2**2) ) THEN
                rmq  = sqrt(kk2(k,j,i))
!               dump = exp(-log10((rmq/vparam2))**2/(2.0*vparam3**2))/rmq**2
                dump = 1.0_GP/rmq**3
              ENDIF
              phase = 2.0*pi*randu(seed); 
              cdump = (cos(phase)+im*sin(phase))*dump
              IF ( ky(j).GT.tiny ) THEN
              C1(k,j,i) = cdump
              C2(k,j,i) = -cdump*kx(i)/ky(j)
              ENDIF
            END DO
          END DO
        END DO
      ELSE
!$omp parallel do if (iend-ista.ge.nth) private (j,k)
        DO i = ista,iend
!$omp parallel do if (iend-ista.lt.nth) private (k)
          DO j = 1,ny
            DO k = 1,nz
              dump = 0.0_GP
              IF ( (kk2(k,j,i).ge.vparam2**2) ) THEN
                rmq  = (kk2(k,j,i))
!               dump = exp(-log10((rmq/vparam2))**2/(2.0*vparam3**2))/rmq**2
                dump = 1.0_GP/rmq**3
              ENDIF
              phase = 2.0*pi*randu(seed); 
              cdump = (cos(phase)+im*sin(phase))*dump
               
              IF ( ky(j).GT.tiny ) THEN
              C1(k,j,i) = cdump
              C2(k,j,i) = -cdump*kx(i)/ky(j)
              ENDIF
            END DO
          END DO
        END DO
      ENDIF
      CALL normalize(C1,C2,C3,sqrt(vparam0)*u0,1,MPI_COMM_WORLD)

!  Add noise to orginal AB(C) state:
      vx = vx + C1
      vy = vy + C2
      CALL normalize(vx,vy,vz,u0,1,MPI_COMM_WORLD)
